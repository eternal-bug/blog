# perl的-M和-I参数

【上集回顾】

上次我们看了一下`-a`与`-F`这两个搭档，男女搭配干活不累~~

这次我们来看一下另外一对搭档~~，参数`-M`和`-I`。

【参数解释】

```text
-M : 导入模块或者编译指示（Pragmas）到Perl单行程序中
-I : 指定一个用来查找模块位置的路径
```

## 用法

```perl
perl -Mmodulename
perl -Idirectory
# 或者
perl -M'modulename'
perl -I'directory'
```

**注意模块名称或者引号要贴着`-M`，中间不能有空格**
 **注意路径或者引号要贴着`-I`，中间不能有空格，如果路径中有空格，务必用引号将其引起来**

## 实例

### -M

**目的**：比如我要查看当前路径或者文件名的基名

```bash
# 首先查看一下当前路径
pwd
# 输出
/c/Users/tian/Desktop

# 比如我想获得该路径的基名，也就是Desktop
# 使用File::Basename中的basename函数
pwd | perl -MFile::Basename -n -e 'print File::Basename::basename($_)'
# 输出
Desktop
```

再来一个例子
 如果有一个文件是 `123.txt`，内容为：

```text
name Chinese math english
Mike 90 89 78
Mary 99 96 90
Tom 80 70 60
```

**目的**：就是需要计算每个人的总成绩，最后将得到的总分放在这一行的末尾输出。之间用空格隔开

```rust
cat 123.txt | perl -MList::Util -a -F"\s+" -n -e '
  # 如果是标题行
  if($. == 1){
    print "@F"," ","total\n";
  }else{
  # 如果不是标题行
    $sum = List::Util::sum(@F[qw/1 2 3/]);
    print "@F"," ","$sum\n";
  }
'
--------------------------------------------------
# 输出
name Chinese math english total
Mike 90 89 78 257
Mary 99 96 90 285
Tom 80 70 60 210
```

### -I

假如在当前目录的lib目录中有一个`test.pm`模块文件，内容为：

```perl
package test;
use strict;
use warnings;

# 这是一个求两个数的和的函数
sub add {
    my ($a,$b) = @_;
    return $a+$b;
}
# 这是一个求两个数的差的函数
sub subtract {
    my ($a,$b) = @_;
    return $a-$b;
}

1;
```

如果说我需要在单行程序中使用它，在lib文件夹的上层位置处我们打开`git for windows`或者终端`cd`到这个位置：

```bash
# 使用-a和-F将echo输入过来的数值分开
# 使用-I指定模块的位置
# 使用-M导入模块
# 使用-e读取从管道传来的数值
echo '123 567' | perl -a -F'\s+' -I./lib -Mtest -n -e '
    $num1 = $F[0];
    $num2 = $F[1];
    # 注意这里函数需要使用全名
    # 比如求123 + 567
    print "add      : ",test::add($num1,$num2),"\n";
    # 比如求123 - 567
    print "subtract : ",test::subtract($num1,$num2),"\n";
'

# 输出为
add      : 690
subtract : -444
```

## 其他实例[选看]

- 使用warnings与strict

```perl
perl -Mwarnings -Mstrict -e '$n = 1;print $n'
```

额，出错了！这么说一直以来我们就写的错误代码？其实假如不使用`warnings`和`strict`的情况下，这个程序没有问题，错就错在一般我们声明新的变量要么加`my` 要么加 `our` 要么 `use vars`，假如这里不加任何声明的词语的话就是相当于在BEGIN块中使用`use vars`来声明该变量。它的作用是它的作用是

> 在当前包中，该变量为全局变量。同时该变量会在该程序运行期间它的值会被保留

什么意思呢？
你可以就这样理解，就是这个变量在哪个地方我都可以用，同时在每每读取一行文件文件之前，它的值都是上一次读取文件进来之后进行运算之后的值。也就是变量不会抹去。实际上之前我们常常这样写，而并没有加上`warnings`和`strict`。

举个例子，比如有一个文件`123.txt`，内容为：

```text
1 Tom 100
2 Jack 98
3 Mary 92
4 Bess 89
5 Betty 79
6 Mike 78
7 Louie 77
8 Emily 75
9 Leo 75
10 Noah 72
```

如果这是一个成绩排名，我想知道有多少人是80分及其以上的人数和80分一下的人数。这里其实一眼就看出来了，但是请配合一下，我们用程序实现一下：

```bash
cat 123.txt | perl -n -a -F"\s+" -e '
  if($F[2] >= 80){
    $n += 1;
  }else{
    $m += 1;
  }
  END{
    print "the number of upper 80 score : $n !\n";
    print "the number of lower 80 score : $n !\n";
  }
'
-----------------------------
# 输出为
the number of upper 80 score : 4 !
the number of lower 80 score : 6 !
```

结果正确，但是如果这样做：

```bash
cat 123.txt | perl -n -a -F"\s+" -e '
  my ($n,$m);
  if($F[2] >= 80){
    $n += 1;
  }else{
    $m += 1;
  }
  END{
    print "the number of upper 80 score : $n !\n";
    print "the number of lower 80 score : $n !\n";
  }
'
-----------------------------
# 输出为
the number of upper 80 score : 1 !
the number of lower 80 score : 1 !
```

结果不对。再试一试这样做：

```bash
cat 123.txt | perl -Mstrict -Mwarnings -n -a -F"\s+" -e '
  # 声明必须放在BEGIN块中，否则每次读取一行文件就会重新声明该全局变量，它之前的值会被抹掉
  BEGIN{
    # 声明变量
    our ($n,$m);
  }
  # 该变量已经存在，那么就为了让该变量在接下来的作用域中默认使用这两个变量
  our $n;
  our $m;
  if($F[2] >= 80){
    $n += 1;
  }else{
    $m += 1;
  }
  END{
    print "the number of upper 80 score : $n !\n";
    print "the number of lower 80 score : $n !\n";
  }
'
-----------------------------
# 输出为
the number of upper 80 score : 4 !
the number of lower 80 score : 6 !
```

其实上述的代码可以这样理解。我们来看一下执行的情况的对照关系。

```perl
                                               |
# perl script                                  | # perl one-liners
use strict;                                    | perl -Mstrict
use warnings;                                  | perl -Mwarnings
                                               | BEGIN{
                                               |   # 这里不能使用my声明，如果使用my声明的话，出了BEGIN块，这两个变量就失效了
our ($n,$m);                                   |   our ($n,$m);
                                               | }
open FILE,"<","123.txt" or die $!;             | # 其实在说-e参数的时候就已经说过，除了BEGIN与END块之外，perl的代码块其实是处在类似于whlie(<FILE>)的循环之中
while(<FILE>){                                 | cat 123.txt |
                                               | 
# 仍然在our的作用范围之内                      | # 出了BEGIN代码块，这里需要再次指明使用这两个变量
                                               | our ($n,$m);
    my @F = split /\s+/,$_;                    | 
    if($F[2] >= 80){                           | if($F[2] >= 80){
        $n += 1;                               |   $n += 1;
    }else{                                     | }else{
        $m += 1;                               |   $m += 1;
    }                                          | }
}                                              |
                                               | END{ 
print "the number of upper 80 score : $n !\n"; |   print "the number of upper 80 score : $n !\n";
print "the number of lower 80 score : $n !\n"; |   print "the number of lower 80 score : $n !\n";
                                               | }
```

也许你会这么写：

```bash
perl -e '
use strict;
use warnings;

my ($n,$m);
open FILE,"<","123.txt" or die $!;
while(<FILE>){
    my @F = split /\s+/,$_;
    if($F[2] >= 80){
        $n += 1;
    }else{
        $m += 1;
    }
}

print "the number of upper 80 score : $n !\n";
print "the number of lower 80 score : $n !\n";
'
```

这么写没错，运行结果很对，但是有点违背初衷了，就是简洁的写法，但是其实解决了问题这个写法也不是不可以。

好了，这个例子主要是想告诉大家，有关-e参数后引号里面的代码的运行情况，其实与strict和warnings没多大干系。
